 /
function# ^waitMaxS events [flags] [mod] ;;events: 1 left, 2 right, 4 middle, 8 X1, 16 X2, 32 move, 64,128 wheel, 256,512 hwheel.   flags: 1 up, 2 eat, 4 can be injected, 8 any mod.   mod: 1 Shift, 2 Ctrl, 4 Alt, 8 Win

 Waits for a mouse button, movement or wheel.
 Returns the event.

 waitMaxS - max number of seconds to wait. Default or 0 is infinite. Error on timeout.
 events - one or more mouse events to wait for.
    For example, to wait for mouse left or right button, events must be 1|2, and the function will return 1 on left click, 2 on right click.
 flags:
    1 - wait for button up event. Default: down. Used only with events 1 - 16.
    2 - eat (disable) the event. If flag 1 is set, also eats corresponding down event, else does not eat corresponding up event.
    4 - also wait for injected events, ie generated by QM macros or other programs.
    8 - ignore mod.
 mod - modifier keys. For example, 1|2 is Shift+Ctrl. If omitted or 0, modifier keys must not be pressed.

 EXAMPLE
 sel WaitForMouseEvent(0 2|4 1|2)
	 case 2 out "right"
	 case 4 out "middle"


type WFMEDATA events flags mod w r
WFMEDATA- __wfme
__wfme.w=0
__wfme.r=0
__wfme.events=events
__wfme.flags=flags
__wfme.mod=mod
int hmouse=SetWindowsHookEx(WH_MOUSE_LL &__WFME_Hook _hinst 0)
opt waitmsg 1
int e
wait waitMaxS V __wfme.w
err e=1
UnhookWindowsHookEx hmouse
if(e) end _error
ret __wfme.r
